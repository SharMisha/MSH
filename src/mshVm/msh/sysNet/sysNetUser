MSH01001    //sysNetUser
    //Модуль обработки входного потока на сервере.
    //Это универсальная программа, может быть использована в любой клиент серверной системе.
    //Она отрабатывает стандартный входной поток. В ней нет специфики конкретного приложения.
    Constant sysCstnt,sysNetCnst;
    //Запрос клиента передается в программу в виде массива аргументов A$
    Return?(A$1=='')|(A$2=='');
    Set %u$1=A$1; //Id Клиента
    Set cm$1=A$2,cm$5=A$2; //Команда клиента
    //Команда 1-й символ в $1, остаток команды в cm$1
    Set $1=cm$1.%strPop(1);
    // в cm$3- 1-поле команды в cm$1- остаток строки
    Set cm$3=cm$1.%fieldPop(CsysfrRou);
    Case?$1; //обработка команды
     //Инициализация клиентов
     CnetcomInit :
      Go?%findLabel(init) init;
     //Инициализация Переменных
     CnetcomValue : // cm$3 - имя переменной, cm$1-атрибуты переменной
      If?cm$3~='';
       //распаковать атрибуты из строки в узел дерева %u[cm$3]
       Kill %u[cm$3];
       While?(cm$1~='');
        Set $2=cm$1.%fieldPop(CsysfrPole),$3=$2.%fieldPop(CsysfrCom);
    	// $3- имя атрибута $2- значение Атрибута
        Set?$3~='' %u[cm$3,$3]=$2;
        Endwhile;
       EndIf;
     //Получить данные cm$2-имя формы cm$4-имя объекта, cm$3 - дополнительная идентификация объекта
     CnetcomGet :
      Set $3=cm$3,cm$2=cm$3.%fieldPop(CsysfrInd),cm$4=cm$3.%fieldPop(CsysfrInd);
      If?(cm$4~='')&%findLabel(get,cm$4);
       //Эта пользовательская программа должна сформировать буфер вывода B$
       Do cm$4.get(cm$2,cm$3);
       //Вывод сервером буфера
       Write SR;
       endIf;
     //Записать данные cm$2-имя формы cm$4-имя объекта, cm$3 - дополнительная идентификация объекта может содержать №строки и №№полей , cm$1 - данные на запись
     CnetcomSet :
      Set cm$2=cm$3.%fieldPop(CsysfrInd),cm$4=cm$3.%fieldPop(CsysfrInd);
      Do?(cm$4~='')&%findLabel(set,cm$4) cm$4.set(cm$2,cm$3,cm$1);
     EndCase;
    Return;
    //если требуется инициализация клиентов, то обработку поместить сюда аргумент Id Клиента в A$1
init:
    Return;
    EndsysNetUser;
